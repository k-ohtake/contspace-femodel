{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "15126ebc-8b3a-4be9-ba62-dc05274b6789",
   "metadata": {},
   "outputs": [],
   "source": [
    "# 2025.4.8 tue. created by Kensuke Ohtake\n",
    "# Footloose-Entrepreneur model in continuous space\n",
    "\n",
    "using Random\n",
    "using Distributions\n",
    "using Plots\n",
    "using Dates\n",
    "using Format\n",
    "using DataFrames\n",
    "using CSV\n",
    "using LinearAlgebra\n",
    "\n",
    "function FE_simulation(pls)\n",
    "\n",
    "    SD = Dates.millisecond(now())\n",
    "    Random.seed!(SD) # Set seed\n",
    "    \n",
    "    # control parameters \n",
    "    sig = pls[1] # elasticity of substitution of manufactured goods\n",
    "    tm = pls[2] # manufacturing transportation cost parameter\n",
    "    \n",
    "    function dist(z)\n",
    "        minimum([abs(z), 2.0 * π - abs(z)])\n",
    "    end\n",
    "    \n",
    "    N = 255 # Number of space fragments\n",
    "    dx = 2.0 * π / convert(Float64, N) # size of space fragments\n",
    "    dt = 0.01 # size of time step\n",
    "    F = 1.0 # fixed input\n",
    "    mu = 0.6 # importance of manufacturing goods\n",
    "    eps = 1e-10 # error\n",
    "    v = 1.0 # adjustment speed of migration\n",
    "    \n",
    "    x = zeros(Float64, N, 1)\n",
    "    for i in 1:N\n",
    "        x[i] = -π + (i-1) * dx\n",
    "    end\n",
    "    \n",
    "    # immobile workers\n",
    "    ph = ones(Float64, (N, 1)) / (2.0 * π)\n",
    "    \n",
    "    # Transport matrix\n",
    "    Ex = zeros(Float64, (N, N))\n",
    "    for i in 1:N\n",
    "        for j in 1:N\n",
    "            Ex[i,j] = exp(- tm * (sig - 1.0) * dist(x[i]-x[j])) # for manufacturing\n",
    "        end\n",
    "    end\n",
    "    \n",
    "    # initial values of manufacturing workers\n",
    "    turb_lm = rand(Uniform(-0.0005, 0.0005), (N, 1)) # perturbation generated by random numbers\n",
    "    turb_lm = turb_lm .- (sum(turb_lm) * dx) / (2.0 * π) # total equals to 0\n",
    "    lm = (ones(Float64, (N, 1)) / (2.0 * π)) + turb_lm # mobile workers: homogeneous state + perturbation\n",
    "    \n",
    "    if sum(Int.(sign.(lm))) != N\n",
    "        println(\"Non-negativity_is_not_satisfied_!!\")\n",
    "    else\n",
    "        ini_lm = copy(lm)\n",
    "        println(\"Initial total mobile workers is \", sum(ini_lm)*dx)\n",
    "    end\n",
    "    \n",
    "    # operator for instantaneous equilibrium\n",
    "    function IntegOperat(nw, immobile, mobile)# Integral operator\n",
    "        # nw: nominal wage\n",
    "        # immobile: phi\n",
    "        # mobile: lambda\n",
    "        Y = (nw .* mobile) .+ immobile\n",
    "        G_integral = Ex * mobile\n",
    "        output = (mu / sig) * Ex * (Y ./ G_integral)\n",
    "        return output\n",
    "    end\n",
    "    \n",
    "    # Right-hand side of the evolution equation\n",
    "    function RHS(candidate_nw, immobile, mobile)\n",
    "        # Candidate_nw: candidate of fixed point of instantaneous equilibrium of nominal wage\n",
    "        # Inputs must be array of which shape is (I, 1) \n",
    "        # solve fixed point problem for instantaneous equilibrium\n",
    "        nominalwage = copy(candidate_nw)\n",
    "        while true\n",
    "            old_nominalwage = copy(nominalwage)\n",
    "            nominalwage = IntegOperat(old_nominalwage, immobile, mobile)\n",
    "            \n",
    "            if maximum(abs.(nominalwage - old_nominalwage)) < eps\n",
    "                break\n",
    "            end\n",
    "        end\n",
    "\n",
    "        # price index\n",
    "        G = ((1.0 / F) * (Ex * mobile * dx)) .^ (1.0 / (1.0 - sig))\n",
    "\n",
    "        # real wage\n",
    "        rw = nominalwage .* (G .^ (-mu)) # real wage\n",
    "    \n",
    "        # Right-hand side of the evolution equation\n",
    "        rhs = v * ((rw .- dot(rw, mobile) * dx) .* mobile)\n",
    "    \n",
    "        return rhs, nominalwage\n",
    "    end\n",
    "    \n",
    "    #= time evolution =#\n",
    "\n",
    "    # time step\n",
    "    t = 0\n",
    "\n",
    "    # initialize candidate for nominal wage of fixed point\n",
    "    w = ones(Float64, (N, 1)) \n",
    "\n",
    "    # Initialize update amount of lambda\n",
    "    dlm = zeros(Float64, (N, 1))\n",
    "\n",
    "    # Iterate until a steady-state solution is reached\n",
    "    while true\n",
    "\n",
    "        # Save previous values\n",
    "        lm_old = copy(lm)\n",
    "        w_old = copy(w)\n",
    "        \n",
    "        # 1-st order Runge Kutta (=Euler) method for the replicator equation \n",
    "        k_1, w = RHS(w_old, ph, lm)\n",
    "        dlm = dt * k_1\n",
    "        lm = lm_old + dlm\n",
    "        \n",
    "        # check nonnegativity\n",
    "        if sum(Int.(sign.(lm))) != N\n",
    "            println(\"Non-negativity_is_not_satisfied_!!\")\n",
    "            break\n",
    "        end\n",
    "        \n",
    "        # check conservation of population\n",
    "        if 1.0 - 1e-10 > sum(lm) * dx\n",
    "            println(\"Conservation_error_!!\")\n",
    "            break\n",
    "        elseif sum(lm) * dx > 1.0 + 1e-10\n",
    "            println(\"Conservation_error_!!\")\n",
    "            break\n",
    "        end\n",
    "\n",
    "        # stationary solution condition\n",
    "        if maximum(abs.(dlm)) < eps\n",
    "            break\n",
    "        end\n",
    "    \n",
    "        t += 1\n",
    "    \n",
    "    end\n",
    "\n",
    "    #= Obtain stationary solutions =#\n",
    "    stationary_w = copy(w)\n",
    "    while true\n",
    "        old_stationary_w = copy(stationary_w)\n",
    "        stationary_w = IntegOperat(old_stationary_w, ph, lm)\n",
    "        if maximum(abs.(stationary_w - old_stationary_w)) < eps\n",
    "            break\n",
    "        end\n",
    "    end\n",
    "\n",
    "    nominal_wage = stationary_w\n",
    "    price_index = ((1 / F) * (Ex * lm) * dx) .^ (1.0 / (1.0 - sig))\n",
    "    real_wage = nominal_wage .* (price_index .^ (-mu))\n",
    "\n",
    "    println(\"Final total mobile workers is \", sum(lm) * dx)\n",
    "\n",
    "    # Plot\n",
    "    DateTime = Dates.format(now(), \"yyyymmddHHMMSS\")# get datetime\n",
    "    plt_x = append!(reshape(x, N), π)\n",
    "    plt_ini_lm = append!(reshape(ini_lm, N), ini_lm[1])\n",
    "    plt_lm = append!(reshape(lm, N), lm[1])\n",
    "    plt_nominal_wage = append!(reshape(nominal_wage, N), nominal_wage[1])\n",
    "    plt_price_index = append!(reshape(price_index, N), price_index[1])\n",
    "    plt_real_wage = append!(reshape(real_wage, N), real_wage[1])\n",
    "    \n",
    "    filename_lm = format(\"{}-pop_tau{}sigma{}mu{}N{}t{}sd{}\", DateTime, tm, sig, mu, N, t, SD)\n",
    "    newfilename_lm = replace(filename_lm, \".\"=>\"p\")\n",
    "    plot(plt_x, plt_lm, label=\"final\", dpi=300)\n",
    "    plot!(plt_x, plt_ini_lm, title=\"mobile workers\", label=\"initial\", dpi=300)\n",
    "    savefig(newfilename_lm * \".png\")\n",
    "    \n",
    "    filename_rw = format(\"{}-rw_tau{}sigma{}mu{}N{}t{}sd{}\", DateTime, tm, sig, mu, N, t, SD)\n",
    "    newfilename_rw = replace(filename_rw, \".\"=>\"p\")\n",
    "    plot(plt_x, plt_real_wage, title=\"real wage\", label=\"\", dpi=300)\n",
    "    savefig(newfilename_rw * \".png\")\n",
    "    \n",
    "    # Output and save data as csv\n",
    "    df = DataFrame(coordinates = plt_x,\n",
    "        initial_pop_lm = plt_ini_lm,\n",
    "        final_pop_lam = plt_lm,\n",
    "        nominal_wage = plt_nominal_wage,\n",
    "        price_index = plt_price_index,\n",
    "        real_wage = plt_real_wage) # Save data as dataframe\n",
    "    filename = format(\"{}_tau{}sigma{}mu{}N{}sd{}\", DateTime, tm, sig, mu, N, SD)\n",
    "    newfilename = replace(filename, \".\"=>\"p\")\n",
    "    CSV.write(newfilename * \".csv\", df)# Write into csv\n",
    "end\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "556c5305-4cc2-49f0-988c-6a5199417bea",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Loop\n",
    "for sig in [2.7, 2.5, 2.4, 2.2, 2.0, 1.7]\n",
    "    params = [sig, 2.0] # set parameters [sigma, tau]\n",
    "    for itr in 1:5\n",
    "        FE_simulation(params)\n",
    "    end\n",
    "end\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "1410808b-98cd-4763-ab69-c6703cfcb3ca",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.4",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.4"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
